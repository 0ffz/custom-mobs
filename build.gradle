import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'org.jetbrains.kotlin.kapt'
    id 'io.github.0ffz.github-packages' version '1.0.3'
}

allprojects {
    group = 'com.mineinabyss'
    version = plugin_version
    if (project.hasProperty("buildNo")) version += ".$buildNo"

    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://erethon.de/repo/' } //HeadLib
        maven { url 'https://hub.spigotmc.org/nexus/content/groups/public/' }
        maven { url 'https://jitpack.io' } //JitPack
        maven { url "https://repo.dmulloy2.net/nexus/repository/public/" }
        maven { url 'https://maven.sk89q.com/repo/' } //WorldGuard/Edit
        maven githubPackage.invoke("MineInAbyss/Idofront")
        maven githubPackage.invoke("MineInAbyss/Guiy")
        maven githubPackage.invoke("MineInAbyss/KotlinSpice")
        mavenLocal()
    }

    compileKotlin { kotlinOptions { jvmTarget = "1.8" } }
    compileTestKotlin { kotlinOptions { jvmTarget = "1.8" } }
}

java {
    setSourceCompatibility(1.8)
    withSourcesJar()
    withJavadocJar()
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:$server_version" //Spigot
    compileOnly "org.spigotmc:spigot:$server_version" // NMS
    compileOnly "de.erethon:headlib:3.0.2"
    compileOnly "com.sk89q.worldguard:worldguard-bukkit:7.0.2" //WorldGuard
    compileOnly "com.comphenix.protocol:ProtocolLib:4.5.0"
    compileOnly "com.github.WesJD.AnvilGUI:anvilgui:5e3ab1f721"
    compileOnly "org.cultofclang.minecraft:kotlinspice:$kotlin_version+"
    compileOnly "org.hexworks.amethyst:amethyst.core-jvm:2020.0.1-PREVIEW"

    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "com.derongan.minecraft:guiy:0.1.0-alpha.7"
    implementation "com.mineinabyss:idofront:0.2.+"
    implementation "com.mineinabyss:idofront-annotation:0.2.+"
    kapt "com.mineinabyss:idofront-processor:0.2.+"
    implementation "net.onedaybeard.bitvector:bitvector-jvm:0.1.4"
    compileOnly project(":processor")
    kapt project(":processor")
}

shadowJar {
    getArchiveBaseName().set("Mobzy")
    getArchiveClassifier().set(null)

    relocate 'com.derongan.minecraft.guiy', "${project.group}.${project.name}.guiy".toLowerCase()
    relocate 'com.mineinabyss.idofront', "${project.group}.${project.name}.idofront".toLowerCase()

    minimize {
        exclude(dependency('de.erethon:headlib:3.0.2'))
        exclude(dependency('com.github.WesJD.AnvilGUI:anvilgui:5e3ab1f721'))
    }
}
build.dependsOn shadowJar

processResources {
    filter ReplaceTokens, tokens: [plugin_version: plugin_version]
}

//Move into plugins folder
if (project.hasProperty("plugin_path") && plugin_path) {
    println("Copying to plugin directory $plugin_path")
    task copyJar(type: Copy) {
        from shadowJar // here it automatically reads jar file produced from jar task
        into plugin_path
    }
    build.dependsOn copyJar
}

publishing {
    repositories {
        maven githubPackage.invoke("MineInAbyss/Mobzy")
    }
    publications {
        gpr(MavenPublication) {
            from components.java
        }
    }
}