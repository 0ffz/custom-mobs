import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'org.jetbrains.kotlin.kapt'
    id 'io.github.0ffz.github-packages' version '1.0.3'
}

allprojects {
    group = 'com.mineinabyss'
    version = plugin_version
    if (project.hasProperty("buildNo")) version += ".$buildNo"

    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://erethon.de/repo/' } //HeadLib
        maven { url 'https://hub.spigotmc.org/nexus/content/groups/public/' }
        maven { url "https://repo.dmulloy2.net/nexus/repository/public/" }
        maven { url 'https://maven.sk89q.com/repo/' } //WorldGuard/Edit
        maven { url 'https://papermc.io/repo/repository/maven-public/' }
        maven { url 'http://nexus.okkero.com/repository/maven-releases/' }
        maven githubPackage.invoke("MineInAbyss/Idofront")
        maven githubPackage.invoke("MineInAbyss/Guiy")
        maven githubPackage.invoke("MineInAbyss/KotlinSpice")
        maven { url 'https://jitpack.io' } //JitPack
        mavenLocal()
    }

    compileKotlin { kotlinOptions { jvmTarget = "1.8" } }
    compileTestKotlin { kotlinOptions { jvmTarget = "1.8" } }
}

java {
    setSourceCompatibility(1.8)
    withSourcesJar()
    withJavadocJar()
}

dependencies {
    compileOnly "com.destroystokyo.paper:paper-api:$server_version"
    compileOnly "org.spigotmc:spigot:$server_version" // NMS

    compileOnly "org.cultofclang.minecraft:kotlinspice:$kotlin_version+"
    implementation "com.derongan.minecraft:guiy:0.1.0-alpha.7"
    implementation "com.mineinabyss:idofront:0.3.+"
    implementation "com.mineinabyss:idofront-annotation:0.3.+"
    kapt "com.mineinabyss:idofront-processor:0.3.+"

    compileOnly "de.erethon:headlib:3.0.2"
    compileOnly "com.sk89q.worldguard:worldguard-bukkit:7.0.2"
    compileOnly "com.comphenix.protocol:ProtocolLib:4.5.0"
    compileOnly "com.github.WesJD.AnvilGUI:anvilgui:5e3ab1f721"

    implementation "com.okkero.skedule:skedule:1.2.6"
    implementation "net.onedaybeard.bitvector:bitvector-jvm:0.1.4"
    implementation 'com.zaxxer:SparseBitSet:1.1'
    implementation 'org.clapper:javautil:3.2.0'
    compileOnly project(":processor")
    kapt project(":processor")
}

shadowJar {
    getArchiveBaseName().set("Mobzy")
    getArchiveClassifier().set(null)

    relocate 'com.derongan.minecraft.guiy', "${project.group}.${project.name}.guiy".toLowerCase()
    relocate 'com.mineinabyss.idofront', "${project.group}.${project.name}.idofront".toLowerCase()

    minimize {
        exclude(dependency('de.erethon:headlib:3.0.2'))
        exclude(dependency('com.github.WesJD.AnvilGUI:anvilgui:5e3ab1f721'))
    }
}
build.dependsOn shadowJar

processResources {
    filter ReplaceTokens, tokens: [plugin_version: plugin_version]
}

//Move into plugins folder
if (project.hasProperty("plugin_path") && plugin_path) {
    println("Copying to plugin directory $plugin_path")
    task copyJar(type: Copy) {
        from shadowJar // here it automatically reads jar file produced from jar task
        into plugin_path
    }
    build.dependsOn copyJar
}

publishing {
    repositories {
        maven githubPackage.invoke("MineInAbyss/Mobzy")
    }
    publications {
        gpr(MavenPublication) {
            from components.java
        }
    }
}